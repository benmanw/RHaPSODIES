#version 430 core

layout (local_size_x = 8, local_size_y = 8) in;
layout (binding = 4, r32ui) uniform restrict uimage2D imgFinalResult;

struct HandModel {
	float modelstate[32];
};

layout(std430, binding = 0) buffer HandModelBuffer
{
	HandModel models[128];
} HandModels;

layout(std430, binding = 4) buffer HandModelIBestBuffer
{
	HandModel models[128];
} HandModelsIBest;

const float Pi = 3.14159265358979323846f;

float Penalty(float fDiff, float fUnion, float fIntersection);
float PenaltyFromReduction(float fDiff, float fUnion, float fIntersection);
float PenaltyPrior(unsigned int model_index);

void UpdateIBest(float fPenalty);

void main() {
	ivec2 texlookup_base = ivec2(
		gl_LocalInvocationID.x * 3,
		gl_LocalInvocationID.y
		);

	float difference_result   = imageLoad(imgFinalResult,
										  texlookup_base + ivec2(0,0))[0] / float(0x7fff);
	float union_result        = imageLoad(imgFinalResult,
										  texlookup_base + ivec2(1,0))[0];
	float intersection_result = imageLoad(imgFinalResult,
										  texlookup_base + ivec2(2,0))[0];

	float fPenalty = Penalty(difference_result,
							 union_result,
							 intersection_result);

	UpdateIBest(fPenalty);
}

float DegToRad(float fDegrees)
{
	return fDegrees / 180.0f * Pi;
}

float Penalty(float fDiff,
			  float fUnion,
			  float fIntersection) {
	float fLambdaK = 2.0;
	unsigned int model_index_base =
		(gl_LocalInvocationID.y * 8 +
		 gl_LocalInvocationID.x) * 2;
		
	float fPenalty =
		PenaltyFromReduction(fDiff, fUnion, fIntersection) +
		fLambdaK * (PenaltyPrior(model_index_base) +
					PenaltyPrior(model_index_base+1));
		
	return fPenalty;
}

float PenaltyFromReduction(float fDiff,
						   float fUnion,
						   float fIntersection) {
	float fLambda = 25;
	float fDepthTerm = fDiff / (fUnion + 1e-6);
	float fSkinTerm = (1 - 2*fIntersection / (fIntersection + fUnion + 1e-6));
	float fPenalty = fLambda * fDepthTerm + fSkinTerm;

	return fPenalty;
}

float PenaltyPrior(unsigned int model_index) {
	float fPenaltySum = 0;
	
	for(int dof = 5; dof < 17; dof += 4) {
		fPenaltySum +=
			min(HandModels.models[model_index].modelstate[dof] -
				HandModels.models[model_index].modelstate[dof+4],
				0.0f);
	}

	fPenaltySum = DegToRad(fPenaltySum);

	return fPenaltySum;
}

void UpdateIBest(float fPenalty) {
	unsigned int model_index =
		(gl_LocalInvocationID.y * 8 +
		 gl_LocalInvocationID.x) * 2;

	float fIBestPenalty = HandModels.models[model_index].modelstate[31];
	
	if(fPenalty <= fIBestPenalty) {
		HandModels.models[model_index].modelstate[31] = fPenalty;

		for(int i = 0; i < 32; ++i) {
			HandModelsIBest.models[model_index].modelstate[i] =
				HandModels.models[model_index].modelstate[i];
			HandModelsIBest.models[model_index+1].modelstate[i] =
				HandModels.models[model_index+1].modelstate[i];
		}
	}
}
